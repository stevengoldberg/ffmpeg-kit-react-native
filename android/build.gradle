buildscript {
  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:8.1.0'
  }
}

apply plugin: 'com.android.library'

static def safePackageName(String packageName) {
  packageName.replace("-lts", "")
}

def safePackageVersion(String packageName) {
  def version = project.properties['ffmpegKit.android.main.version']
  def ltsVersion = project.properties['ffmpegKit.android.lts.version']
  packageName.contains("-lts") ? ltsVersion + ".LTS" : version
}

def safeExtGet(String prop, String fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
  if (project.android.hasProperty("namespace")) {
    namespace 'com.arthenica.ffmpegkit.reactnative'
  }

  compileSdkVersion 33
  ndkVersion rootProject.ext.ndkVersion

  defaultConfig {
    minSdkVersion safeExtGet('ffmpegKitPackage', 'https').contains("-lts") ? 16 : 24
    targetSdkVersion 33
    versionCode 602
    versionName "6.0.2"
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }
  lintOptions {
    disable 'GradleCompatible'
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }
}

repositories {
  mavenCentral()
  google()

  def found = false
  def defaultDir = null
  def androidSourcesName = 'React Native sources'

  if (rootProject.ext.has('reactNativeAndroidRoot')) {
    defaultDir = rootProject.ext.get('reactNativeAndroidRoot')
  } else {
    defaultDir = new File(
      projectDir,
      'node_modules/react-native/android'
    )
  }

  if (defaultDir.exists()) {
    maven {
      url defaultDir.toString()
      name androidSourcesName
    }

    logger.info(":${project.name}:reactNativeAndroidRoot ${defaultDir.canonicalPath}")
    found = true
  } else {
    def parentDir = rootProject.projectDir

    1.upto(5, {
      if (found) return true
      parentDir = parentDir.parentFile

      def androidSourcesDir = new File(
        parentDir,
        'node_modules/react-native'
      )

      def androidPrebuiltBinaryDir = new File(
        parentDir,
        'node_modules/react-native/android'
      )

      if (androidPrebuiltBinaryDir.exists()) {
        maven {
          url androidPrebuiltBinaryDir.toString()
          name androidSourcesName
        }

        logger.info(":${project.name}:reactNativeAndroidRoot ${androidPrebuiltBinaryDir.canonicalPath}")
        found = true
      } else if (androidSourcesDir.exists()) {
        maven {
          url androidSourcesDir.toString()
          name androidSourcesName
        }

        logger.info(":${project.name}:reactNativeAndroidRoot ${androidSourcesDir.canonicalPath}")
        found = true
      }
    })
  }

  if (!found) {
    throw new GradleException(
      "${project.name}: unable to locate React Native android sources. " +
        "Ensure you have you installed React Native as a dependency in your project and try again."
    )
  }
}

dependencies {
  api 'com.facebook.react:react-native:+'
  //implementation 'com.arthenica:ffmpeg-kit-' + safePackageName(safeExtGet('ffmpegKitPackage', 'https')) + ':' + safePackageVersion(safeExtGet('ffmpegKitPackage', 'https'))
  //project(':ffmpeg-kit')
}

tasks.register("buildFFmpegKit") {
  doLast {
    if (android.ndkDirectory == null || android.ndkDirectory.path == "null" || !android.ndkDirectory.exists()) {
      throw new GradleException("Invalid NDK directory ${android.ndkDirectory}")
    }
    println "Attempting to build ffmpeg-kit from source..."
    println("ANDROID_SDK_ROOT = ${android.sdkDirectory.absolutePath}")
    println("ANDROID_NDK_ROOT = ${android.ndkDirectory.absolutePath}")
    exec {
      workingDir "${project.projectDir}/../ffmpreg-kit"
      commandLine "mkdir", "-p", "./android/ffmpeg-kit-android-lib/src/main/res/raw"
    }
    exec {
      workingDir "${project.projectDir}/../ffmpreg-kit"
      commandLine "touch", "./android/ffmpeg-kit-android-lib/src/main/res/raw/license_cpu_features.txt"
    }
    exec {
      workingDir "${project.projectDir}/../ffmpreg-kit"
      environment "ANDROID_SDK_ROOT", android.sdkDirectory.absolutePath
      environment "ANDROID_NDK_ROOT", android.ndkDirectory.absolutePath
      commandLine "bash", "./android.sh"
    }
  }
}

preBuild.dependsOn buildFFmpegKit
